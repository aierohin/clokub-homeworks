# Домашнее задание к занятию "15.1. Организация сети"

---
## Задание 1. Яндекс.Облако (обязательное к выполнению)

1. Создать VPC.
- Создать пустую VPC. Выбрать зону.
2. Публичная подсеть.
- Создать в vpc subnet с названием public, сетью 192.168.10.0/24.
- Создать в этой подсети NAT-инстанс, присвоив ему адрес 192.168.10.254. В качестве image_id использовать fd80mrhj8fl2oe87o4e1
- Создать в этой публичной подсети виртуалку с публичным IP и подключиться к ней, убедиться что есть доступ к интернету.
3. Приватная подсеть.
- Создать в vpc subnet с названием private, сетью 192.168.20.0/24.
- Создать route table. Добавить статический маршрут, направляющий весь исходящий трафик private сети в NAT-инстанс
- Создать в этой приватной подсети виртуалку с внутренним IP, подключиться к ней через виртуалку, созданную ранее и убедиться что есть доступ к интернету
---
```
terraform {
  required_providers {
    yandex = {
      source = "yandex-cloud/yandex"
    }
  }
}

data "yandex_compute_image" "centos_image" {
  family = "centos-7"
}

locals {
  nat_instance_image_id = "fd80mrhj8fl2oe87o4e1"
  cidr_internet         = "0.0.0.0/0"            
}

provider "yandex" {
  service_account_key_file = "/home/erohin/terraform-cloud/key.json"
  cloud_id  = "<b1gp7k458hlu48fmqj2v>"
  folder_id = "b1g7umb836h4foki8gu0"
}

resource "yandex_vpc_network" "network" {
  name = "network"
}

resource "yandex_vpc_subnet" "public" {
  name           = "public"
  zone           = "ru-central1-a"
  network_id     = yandex_vpc_network.network.id
  v4_cidr_blocks = ["192.168.10.0/24"]
}

resource "yandex_vpc_subnet" "private" {
  name           = "private"
  zone           = "ru-central1-a"
  network_id     = yandex_vpc_network.network.id
  v4_cidr_blocks = ["192.168.20.0/24"]
  route_table_id = yandex_vpc_route_table.route-table-nat.id
}

resource "yandex_vpc_route_table" "route-table-nat" {
  description = "Route table for Data Proc cluster subnet" # All requests can be forwarded to the NAT instance IP address.
  name        = "route-table-nat"

  depends_on = [
    yandex_compute_instance.nat-instance-vm
  ]

  network_id = yandex_vpc_network.network.id

  static_route {
    destination_prefix = local.cidr_internet
    next_hop_address   = yandex_compute_instance.nat-instance-vm.network_interface.0.ip_address
  }
}

resource "yandex_compute_instance" "nat-instance-vm" {
  description = "NAT instance VM"
  name        = "nat-instance-vm"
  platform_id = "standard-v3" # Intel Ice Lake
  zone        = "ru-central1-a"

  resources {
    cores  = 2 # vCPU
    memory = 4 # GB
  }

  boot_disk {
    initialize_params {
      image_id = local.nat_instance_image_id
    }
  }

  network_interface {
    subnet_id  = yandex_vpc_subnet.public.id
    nat        = true # Required for connection from the Internet.
    ip_address = "192.168.10.254"
  }

  metadata = {
    ssh-key = "${file("~/.ssh/id_rsa.pub")}"
  }
}

resource "yandex_compute_instance" "public-vm" {
  name = "public-vm"
  zone        = "ru-central1-a"
  allow_stopping_for_update = true

  resources {
    cores  = 2
    memory = 2
  }

  boot_disk {
    initialize_params {
      image_id = data.yandex_compute_image.centos_image.id
    }
  }

  network_interface {
    subnet_id = yandex_vpc_subnet.public.id
    nat       = true
  }

  metadata = {
    ssh-key = "${file("~/.ssh/id_rsa.pub")}"
    serial-port-enable = 1
  }
}

resource "yandex_compute_instance" "private-vm" {
  name = "private-vm"
  zone        = "ru-central1-a"

  resources {
    cores  = 2
    memory = 2
  }

  boot_disk {
    initialize_params {
      image_id = data.yandex_compute_image.centos_image.id
    }
  }

  network_interface {
    subnet_id = yandex_vpc_subnet.private.id
    nat       = false
    ip_address = "192.168.20.200"
  }

  metadata = {
    ssh-key = "${file("~/.ssh/id_rsa.pub")}"
  }
}
```
```
erohin@erohin-VirtualBox:~/terraform-cloud$ ssh yc-user@62.84.117.229
[yc-user@fhmjllga44qn3rdhj3sh ~]$ ping 8.8.8.8
PING 8.8.8.8 (8.8.8.8) 56(84) bytes of data.
64 bytes from 8.8.8.8: icmp_seq=1 ttl=61 time=16.0 ms
64 bytes from 8.8.8.8: icmp_seq=2 ttl=61 time=15.6 ms
64 bytes from 8.8.8.8: icmp_seq=3 ttl=61 time=15.4 ms
^C
--- 8.8.8.8 ping statistics ---
3 packets transmitted, 3 received, 0% packet loss, time 2003ms
rtt min/avg/max/mdev = 15.453/15.719/16.071/0.259 ms
[yc-user@fhmjllga44qn3rdhj3sh ~]$ 

[yc-user@fhmjllga44qn3rdhj3sh ~]$ ssh 192.168.20.200 -i key.txt 
[yc-user@fhmisn9bfii4akrakuls ~]$ ping 8.8.8.8
PING 8.8.8.8 (8.8.8.8) 56(84) bytes of data.
64 bytes from 8.8.8.8: icmp_seq=1 ttl=59 time=22.1 ms
64 bytes from 8.8.8.8: icmp_seq=2 ttl=59 time=20.9 ms
64 bytes from 8.8.8.8: icmp_seq=3 ttl=59 time=21.1 ms
64 bytes from 8.8.8.8: icmp_seq=4 ttl=59 time=21.0 ms
^C
--- 8.8.8.8 ping statistics ---
4 packets transmitted, 4 received, 0% packet loss, time 3003ms
rtt min/avg/max/mdev = 20.961/21.323/22.103/0.478 ms
```
![2022-09-07_16-41-43](https://user-images.githubusercontent.com/88886716/188894444-2dbfa710-2cc2-4906-8424-fb25549e9def.png)
![2022-09-07_16-42-20](https://user-images.githubusercontent.com/88886716/188894477-1fd81fa2-5e46-4c5c-8413-b66b40eeaa49.png)
![2022-09-07_16-42-50](https://user-images.githubusercontent.com/88886716/188894499-bd5d846f-b9e1-48a8-a03c-301c5161f153.png)

